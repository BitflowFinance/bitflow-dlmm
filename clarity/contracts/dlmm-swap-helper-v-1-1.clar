;; dlmm-swap-helper-v-1-1

;; Use DLMM pool trait and SIP 010 trait
(use-trait dlmm-pool-trait .dlmm-pool-trait-v-1-1.dlmm-pool-trait)
(use-trait sip-010-trait .sip-010-trait-ft-standard-v-1-1.sip-010-trait)

;; Error constants
(define-constant ERR_NO_RESULT_DATA (err u2001))
(define-constant ERR_BIN_SLIPPAGE (err u2002))
(define-constant ERR_MINIMUM_RECEIVED (err u2003))
(define-constant ERR_NO_ACTIVE_BIN_DATA (err u2004))

;; Swap through multiple bins in multiple pools
(define-public (swap-helper
    (swaps (list 120 {pool-trait: <dlmm-pool-trait>, x-token-trait: <sip-010-trait>, y-token-trait: <sip-010-trait>, bin-id: int, amount: uint, x-for-y: bool}))
    (min-received uint) (max-unfavorable-bins uint)
  )
  (let (
    (swap-result (try! (fold fold-swap-helper swaps (ok {received: u0, unfavorable: u0}))))
    (received (get received swap-result))
  )
    (asserts! (<= (get unfavorable swap-result) max-unfavorable-bins) ERR_BIN_SLIPPAGE)
    (asserts! (>= received min-received) ERR_MINIMUM_RECEIVED)
    (ok received)
  )
)

;; Fold function to swap through multiple bins in multiple pools
(define-private (fold-swap-helper
    (swap {pool-trait: <dlmm-pool-trait>, x-token-trait: <sip-010-trait>, y-token-trait: <sip-010-trait>, bin-id: int, amount: uint, x-for-y: bool})
    (result (response {received: uint, unfavorable: uint} uint))
  )
  (let (
      (result-data (unwrap! result ERR_NO_RESULT_DATA))
      (pool-trait (get pool-trait swap))
      (active-bin-id (unwrap! (contract-call? pool-trait get-active-bin-id) ERR_NO_ACTIVE_BIN_DATA))
      (bin-id-delta (- active-bin-id (get bin-id swap)))
      (is-unfavorable (if (get x-for-y swap) (> bin-id-delta 0) (< bin-id-delta 0)))
      (swap-result (if (get x-for-y swap)
                       (try! (contract-call? .dlmm-core-v-1-1 swap-x-for-y pool-trait (get x-token-trait swap) (get y-token-trait swap) active-bin-id (get amount swap)))
                       (try! (contract-call? .dlmm-core-v-1-1 swap-y-for-x pool-trait (get x-token-trait swap) (get y-token-trait swap) active-bin-id (get amount swap)))))
  )
    (ok {
      received: (+ (get received result-data) swap-result),
      unfavorable: (+ (get unfavorable result-data) (if is-unfavorable (abs-int bin-id-delta) u0))
    })
  )
)

;; Get absolute value of a signed int as uint
(define-private (abs-int (value int))
  (to-uint (if (>= value 0) value (- value)))
)